## ---------------------------------------------------------------------
##
## Copyright (c) 2020 - 2021 by the IBAMR developers
## All rights reserved.
##
## This file is part of IBAMR.
##
## IBAMR is free software and is distributed under the 3-clause BSD
## license. The full text of the license can be found in the file
## COPYRIGHT at the top level directory of IBAMR.
##
## ---------------------------------------------------------------------

# This file is a template that is populated by CMake with the actual locations
# of external dependencies and also the file containing information on IBAMR's
# own targets.
@PACKAGE_INIT@

SET(MPI_ROOT "@MPI_ROOT@")
FIND_PACKAGE(MPI REQUIRED)

IF(NOT @IBAMR_USE_BUNDLED_BOOST@)
  SET(Boost_ROOT "@BOOST_ROOT@")
  FIND_PACKAGE(Boost 1.57 REQUIRED)

  # We do not want to set BOOST_ALL_NO_LIB in case IBAMR is linked to other
  # libraries that *do* link against boost libraries
  GET_TARGET_PROPERTY(_boost_definitions Boost::headers
    INTERFACE_COMPILE_DEFINITIONS)
  LIST(REMOVE_ITEM _boost_definitions "BOOST_ALL_NO_LIB")
  SET_TARGET_PROPERTIES(Boost::headers PROPERTIES INTERFACE_COMPILE_DEFINITIONS
    "${_boost_definitions}")
ENDIF()

IF(NOT @IBAMR_USE_BUNDLED_EIGEN3@)
  SET(Eigen3_ROOT "@EIGEN3_ROOT@")
  FIND_PACKAGE(Eigen3 3.2.5 REQUIRED)
ENDIF()

# non-bundled muParser is not handled as a package so we don't set it up again
# here

IF(@IBAMR_HAVE_SILO@)
  SET(ZLIB_ROOT "@ZLIB_ROOT@")
  FIND_PACKAGE(ZLIB REQUIRED)
ENDIF()

SET(HDF5_ROOT "@HDF5_ROOT@")
FIND_PACKAGE(HDF5 REQUIRED)

INCLUDE(${CMAKE_CURRENT_LIST_DIR}/IBAMRTargets.cmake)

# Attempt to avoid conflicts between potential system copies of bundled
# dependencies and the actual bundled dependencies by putting their include
# directories at the front:

IF(@IBAMR_USE_BUNDLED_BOOST@)
  GET_TARGET_PROPERTY(_boost_includes IBAMR::BUNDLED_BOOST
    INTERFACE_INCLUDE_DIRECTORIES)
  TARGET_INCLUDE_DIRECTORIES(IBAMR::IBTK2d BEFORE INTERFACE "${_boost_includes}")
  TARGET_INCLUDE_DIRECTORIES(IBAMR::IBTK3d BEFORE INTERFACE "${_boost_includes}")
ENDIF()

IF(@IBAMR_USE_BUNDLED_EIGEN3@)
  GET_TARGET_PROPERTY(_eigen3_includes IBAMR::BUNDLED_EIGEN3
    INTERFACE_INCLUDE_DIRECTORIES)
  TARGET_INCLUDE_DIRECTORIES(IBAMR::IBTK2d BEFORE INTERFACE "${_eigen3_includes}")
  TARGET_INCLUDE_DIRECTORIES(IBAMR::IBTK3d BEFORE INTERFACE "${_eigen3_includes}")
ENDIF()

IF(@IBAMR_USE_BUNDLED_MUPARSER@)
  GET_TARGET_PROPERTY(_muparser_includes IBAMR::BUNDLED_MUPARSER
    INTERFACE_INCLUDE_DIRECTORIES)
  TARGET_INCLUDE_DIRECTORIES(IBAMR::IBTK2d BEFORE INTERFACE "${_muparser_includes}")
  TARGET_INCLUDE_DIRECTORIES(IBAMR::IBTK3d BEFORE INTERFACE "${_muparser_includes}")
ENDIF()
